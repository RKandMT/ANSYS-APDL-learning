!电热丝半径r=1.015mm，电阻率80*10^(-6)Ω·cm，导热率19.03W/(m·K)，稳态时，通过电热丝的电流为150A，取轴长为1.5mm，确定中心线上的温度较表面温度高多少？
!PS:Q=I^2R/pai*r^2l

FINISH
/CLEAR
/PREP7
!-----------参数设置--------------
R=0.001015!电线半径
Q=1.718E9!热生成率
LB=19.03!材料导热率
!-----------定义单元--------------
ET,1,SOLID70
MP,KXX,1,LB!导热率
!--------几何建模&网格划分--------
CYLIND,R,0,0,0.0015,0,90
ESIZE,0.00025,0
MSHKEY,1
VMESH,1
!-------------施加荷载------------
BFV,1,HGEN,Q
DA,3,TEMP,0
FINISH
!-------------求解----------------
/SOLU
ANTYPE,STATIC
TIME,1
SOLVE
FINISH
!-------------后处理--------------
/POST1
SET,LAST
/POST1
FLST,2,9,1!指定拾取操作（GUI）所需的数据:命令2,选择对象数量,以节点号选择
FITEM,2,1!选择节点1
FITEM,2,3
FITEM,2,4
FITEM,2,5
FITEM,2,6
FITEM,2,7
FITEM,2,8
FITEM,2,9
FITEM,2,2
PATH,R2,9,30,20!建立径向路径：名称R1,路径点数量7,可映射到此路径的数据集数：默认30,相邻点之间的分割数：默认20
PPATH,P51X,1!???
PATH,STAT!显示路径设置状态
PDEF,TR2,TEMP,,AVG!向所定义路径映射温度分析结果
PLPATH,TR2!显示沿温度变化曲线
PLPAGM,TR2,0.01,BLANK!在几何模型上显示径向温度场分布云图
PLNSOL,TEMP,,0!显示温度场分布云图
*GET,T0,NODE,1,TEMP!获取电热丝中心线上1号节点温度
*GET,T2,NODE,2,TEMP!获取电热丝表面上2号节点温度
T=T0-T2!有限元计算结果
LT=(R**2)*Q/(4*LB)!理论计算结果
ER=1-LT/T!计算误差
*STATUS,PARM!列出所有参数
